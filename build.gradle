plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.4'
    id 'maven-publish'
}

group = 'cn.xiaozhou233.xiaoyubot'
version = '2.1.0'

repositories {
    mavenCentral()
    if (!System.getenv().containsKey("CI")) {
    maven {
        name "gitea"
        allowInsecureProtocol = true
        url "http://192.168.2.222:13000/api/packages/xiaozhou233/maven"
    }}

    maven {
        url = uri("https://maven.pkg.github.com/xiaozhou233/OneBot-JavaBean")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GPR_KEY")
        }
    }
}

dependencies {
    // Log System
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    implementation 'org.slf4j:slf4j-api:2.0.13'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    implementation 'ch.qos.logback:logback-classic:1.5.15'

    // Network
    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '5.0.0-alpha.14'

    // Data
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation("com.fasterxml.jackson.core:jackson-core:2.18.3")
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    implementation("com.fasterxml.jackson.core:jackson-databind:2.18.3")
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.18.3")

    implementation 'onebot11:onebot-javabean:1.1'
}

// build artifact (with dependencies)
shadowJar {
    destinationDirectory = file("${project.projectDir}/out")
    archiveFileName.set("${project.name}_${project.version}.jar")
    manifest {
        attributes(
                'Implementation-Title': 'XiaoYuBotX',
                'Implementation-Version': version,
                'Main-Class': 'cn.xiaozhou233.xiaoyubot.Bootstrap'
        )
    }
}



tasks.named('build') {
    // clean
    dependsOn(tasks.clean)
    // build
    dependsOn(tasks.shadowJar)
}


publishing {
    repositories {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = 'xiaoyubotx'
                artifactId = 'xiaoyubotx'
            }
        }


        // Local Server
        maven {
            name = "Gitea"
            url = uri("http://192.168.2.222:13000/api/packages/xiaozhou233/maven")
            allowInsecureProtocol = true

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token " + (findProperty("gitea.token") ?: "")
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}